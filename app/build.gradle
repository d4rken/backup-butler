apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.bugsnag.android.gradle'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("GMT+1"))

android {
    def packageName = "eu.darken.bb"

    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "${packageName}"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        buildConfigField "String", "GITSHA", "\"${gitSha}\""
        buildConfigField "String", "BUILDTIME", "\"${buildTime}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {}
    }
    def signingPropFile = new File(System.properties['user.home'], ".appconfig/${packageName}/signing.properties")
    if (signingPropFile.canRead()) {
        Properties signingProps = new Properties()
        signingProps.load(new FileInputStream(signingPropFile))
        signingConfigs {
            release {
                storeFile new File(signingProps['release.storePath'])
                keyAlias signingProps['release.keyAlias']
                storePassword signingProps['release.storePassword']
                keyPassword signingProps['release.keyPassword']
            }
        }
    }

    buildTypes {
        def proguardRulesRelease = fileTree(dir: 'proguard', include: ['*.pro']).asList().toArray()
        project.logger.lifecycle("Picked up proguard release files: " + proguardRulesRelease.toString())

        debug {
            lintOptions {

            }
            ext.enableBugsnag = true
            buildConfigField "boolean", "BETA", "true"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles proguardRulesRelease
            proguardFiles 'proguard-rules-debug.pro'
        }
        beta {
            signingConfig signingConfigs.release
            lintOptions {
                disable 'MissingTranslation'
                disable 'ExtraTranslation'
            }
            ext.enableBugsnag = true
            buildConfigField "boolean", "BETA", "true"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles proguardRulesRelease
            matchingFallbacks = ['release']
        }
        release {
            signingConfig signingConfigs.release
            lintOptions {
                abortOnError true
                fatal 'StopShip'
            }
            ext.enableBugsnag = true
            buildConfigField "boolean", "BETA", "false"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles proguardRulesRelease
            javaCompileOptions.annotationProcessorOptions.arguments['butterknife.debuggable'] = 'false'
        }
        applicationVariants.all { variant ->
            if (variant.buildType.name == "debug") {
                variant.mergedFlavor.resourceConfigurations.clear()
                variant.mergedFlavor.resourceConfigurations.add("en")
                variant.mergedFlavor.resourceConfigurations.add("de")
            } else if (variant.buildType.name != "debug") {
                variant.outputs.each { output ->
                    output.outputFileName = "${packageName}-v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ")-" + variant.buildType.name.toUpperCase() + "-" + gitSha + ".apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    androidExtensions {
        experimental = true
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
            freeCompilerArgs = ['-XXLanguage:+InlineClasses']
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin.core}"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlin.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx3:${versions.kotlin.coroutines}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlin.coroutines}"

    // Support libs
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.2.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:1.0.0-rc01'
    implementation 'androidx.activity:activity-ktx:1.1.0-rc01'
    implementation 'androidx.fragment:fragment-ktx:1.2.0-rc01'
    implementation 'androidx.viewpager2:viewpager2:1.0.0-rc01'
    implementation 'androidx.recyclerview:recyclerview-selection:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.core:core-ktx:1.1.0'

    implementation "androidx.fragment:fragment:1.2.0-rc01"

    implementation "androidx.navigation:navigation-fragment-ktx:${versions.androidx.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.androidx.navigation}"

    implementation 'com.airbnb.android:lottie:3.1.0'

    def work_version = "2.4.0-rc01"
    implementation "androidx.work:work-runtime:${work_version}"
    implementation "androidx.work:work-rxjava3:${work_version}"
    testImplementation "androidx.work:work-testing:${work_version}"

    // Network
    implementation("com.squareup.okio:okio:2.4.1")

    // Serialization
    implementation "com.squareup.moshi:moshi:${versions.moshi}"
    implementation "com.squareup.moshi:moshi-adapters:${versions.moshi}"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}"

    implementation 'org.apache.commons:commons-compress:1.19'

    // ROOM
    def room_version = "2.2.1"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    //Dagger
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
    implementation "com.google.dagger:dagger:${versions.dagger}"
    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${versions.dagger}"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // Root
    implementation project(':bb-root')
    implementation 'eu.chainfire:libsuperuser:+'

    // Rx
    implementation "io.reactivex.rxjava3:rxjava:${versions.rxjava}"
    implementation "io.reactivex.rxjava3:rxandroid:${versions.rxandroid}"
    implementation "io.reactivex.rxjava3:rxkotlin:${versions.rxkotlin}"
    implementation 'com.jakewharton.rx3:replaying-share:3.0.0'
    implementation "com.jakewharton.rx3:replaying-share-kotlin:3.0.0"
    implementation "com.github.akarnokd:rxjava3-bridge:3.0.0"

    implementation "com.jakewharton.rxbinding4:rxbinding:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-core:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-appcompat:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-drawerlayout:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-leanback:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-recyclerview:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-slidingpanelayout:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-swiperefreshlayout:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-viewpager:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-viewpager2:${versions.rxbindings}"
    implementation "com.jakewharton.rxbinding4:rxbinding-material:${versions.rxbindings}"

    implementation "eu.darken.rxshell:core:${versions.rxshell}"
    implementation "eu.darken.rxshell:root:${versions.rxshell}"

    // Debug
    implementation "com.jakewharton.timber:timber:${versions.timber}"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
    implementation "com.uber.rxdogtag:rxdogtag:0.2.0"
    implementation 'com.bugsnag:bugsnag-android:5.9.2'

    //Butterknife
    implementation "com.jakewharton:butterknife:${versions.butterknife}"
    kapt "com.jakewharton:butterknife-compiler:${versions.butterknife}"

    /**
     * Unit testing
     */
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.7.0")

    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation "io.kotest:kotest-runner-junit5:4.6.2"
    testImplementation "io.kotest:kotest-assertions-core-jvm:4.6.2"
    testImplementation "io.kotest:kotest-property-jvm:4.6.2"

    testImplementation 'org.awaitility:awaitility:4.0.2'
    testImplementation 'org.awaitility:awaitility-kotlin:4.0.2'

    testImplementation 'androidx.test.ext:junit:1.1.3'

    // A specific version just used within tests to format output when we don't want or can mock JSONObject
    //noinspection GradleDependency
    testImplementation 'org.json:json:20140107'

    testImplementation 'org.robolectric:robolectric:4.3.1'
    // Robolectric requires jUnit4
    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation "junit:junit:4.13"
    testImplementation "org.robolectric:robolectric:4.3.1"


    /**
     * Instrumentation testing
     */
    androidTestImplementation "org.mockito:mockito-core:2.26.0"
    androidTestImplementation 'org.mockito:mockito-android:2.23.0'
    androidTestImplementation 'com.github.tmurakami:dexopener:1.0.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'
}